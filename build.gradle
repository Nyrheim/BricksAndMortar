plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "5.2.0"
    id "maven-publish"
}

group "net.nyrheim"
version "1.1.0-SNAPSHOT"

def githubUsername = ""
def githubToken = ""
def githubPropertiesFile = new File(project.projectDir, "github.properties")
if (githubPropertiesFile.exists()) {
    def githubProperties = new Properties()
    githubProperties.load(new FileInputStream(githubPropertiesFile))
    githubUsername = githubProperties.get("github-username")
    githubToken = githubProperties.get("github-token")
}

repositories {
    mavenCentral()
    maven { url "https://hub.spigotmc.org/nexus/content/groups/public/" }
    maven { url "https://repo.rpkit.com/repository/maven-public/" }
    maven {
        url "https://maven.pkg.github.com/Nyrheim/PenAndPaper"
        credentials {
            username githubUsername
            password githubToken
        }
    }
}

dependencies {
    implementation group: "org.spigotmc", name: "spigot-api", version: "1.14.4-R0.1-SNAPSHOT"
    implementation group: "mysql", name: "mysql-connector-java", version: "8.0.20"
    implementation group: "com.zaxxer", name: "HikariCP", version: "3.4.5"
    implementation group: "com.typesafe", name: "config", version: "1.4.0"
    implementation group: "org.jooq", name: "jooq", version: "3.13.2"
    implementation group: "org.slf4j", name: "slf4j-jdk14", version: "1.7.30"
    implementation group: "org.ehcache", name: "ehcache", version: "3.8.1"

    implementation group: "org.jetbrains", name: "annotations", version: "19.0.0"
    implementation group: "org.jetbrains.kotlin", name: "kotlin-stdlib-jdk8", version: "1.3.72"
    implementation group: "org.jetbrains.kotlin", name: "kotlin-reflect", version: "1.3.72"
    implementation group: "net.nyrheim", name: "PenAndPaper", version: "1.1.0"
    implementation group: "com.rpkit", name: "rpk-core", version: "1.9.0"
    implementation group: "com.rpkit", name: "rpk-core-bukkit", version: "1.9.0"
    implementation group: "com.rpkit", name: "rpk-player-lib-bukkit", version: "1.9.0"
    implementation group: "com.rpkit", name: "rpk-item-quality-lib-bukkit", version: "1.9.0"
    implementation group: "com.rpkit", name: "rpk-selection-lib-bukkit", version: "1.9.0"
    testImplementation group: "junit", name: "junit", version: "4.12"
}
import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    filter ReplaceTokens, tokens: [
            "version": version
    ]
}

shadowJar {
    dependencies {
        include(dependency("mysql:mysql-connector-java"))
        include(dependency("com.google.protobuf:protobuf-java"))

        include(dependency("com.zaxxer:HikariCP"))

        include(dependency("org.jooq:jooq"))
        include(dependency("org.reactivestreams:reactive-streams"))
        include(dependency("javax.xml.bind:jaxb-api"))
        include(dependency("javax.activation:javax-activation-api"))

        include(dependency("org.slf4j:slf4j-jdk14"))
        include(dependency("org.slf4j:slf4j-api"))

        include(dependency("org.ehcache:ehcache"))
        include(dependency("org.glassfish.jaxb:jaxb-runtime"))
        include(dependency("org.glassfish.jaxb:txw2"))
        include(dependency("com.sun.istack:istack-commons-runtime"))
        include(dependency("org.jvnet.staxex:stax-ex"))
        include(dependency("com.sun.xml.fastinfoset:FastInfoSet"))
        include(dependency("javax.activation:javax.activation-api"))
    }

    relocate "com.mysql", "net.nyrheim.bricksandmortar.shadow.com.mysql"
    relocate "com.google", "net.nyrheim.bricksandmortar.shadow.com.google"
    relocate "google", "net.nyrheim.bricksandmortar.shadow.google"

    relocate "com.zaxxer", "net.nyrheim.bricksandmortar.shadow.com.zaxxer"

    relocate "org.jooq", "net.nyrheim.bricksandmortar.shadow.org.jooq"
    relocate "xsd", "net.nyrheim.bricksandmortar.shadow.xsd"
    relocate "migrations", "net.nyrheim.bricksandmortar.shadow.migrations"
    relocate "org.reactivestreams", "net.nyrheim.bricksandmortar.shadow.org.reactivestreams"
    relocate "javax.xml.bind", "net.nyrheim.bricksandmortar.shadow.javax.xml.bind"
    relocate "javax.activation", "net.nyrheim.bricksandmortar.shadow.javax.activation"

    relocate "org.slf4j", "net.nyrheim.bricksandmortar.shadow.org.slf4j"

    relocate "org.ehcache", "net.nyrheim.bricksandmortar.shadow.org.ehcache"
    relocate "org.terracotta", "net.nyrheim.bricksandmortar.shadow.org.terracotta"
    relocate "com.sun.xml.bind", "net.nyrheim.bricksandmortar.shadow.com.sun.xml.bind"
    relocate "com.sun.xml.txw2", "net.nyrheim.bricksandmortar.shadow.com.sun.xml.txw2"
    relocate "com.sun.istack", "net.nyrheim.bricksandmortar.shadow.com.sun.istack"
    relocate "org.jvnet.staxex", "net.nyrheim.bricksandmortar.shadow.org.jvnet.staxex"
    relocate "com.sun.xml.fastinfoset", "net.nyrheim.bricksandmortar.shadow.com.sun.xml.fastinfoset"
    relocate "javax.activation", "net.nyrheim.bricksandmortar.shadow.javax.activation"

    mergeServiceFiles()
}

tasks.build.dependsOn tasks.shadowJar

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Nyrheim/BricksAndMortar")
            credentials {
                username = githubUsername
                password = githubToken
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
